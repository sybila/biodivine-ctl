buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'application'

group = "com.github.sybila"
archivesBaseName = "pithya"
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}


configurations.all {    //refresh snapshots on every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'com.google.code.gson:gson:2.6.2'
    //ODE and CTL modules do not propagate this dependency, because it's part of the plugin
    compile 'org.antlr:antlr4-runtime:4.6'
    //BioDivine modules. We list all, since that will override possible old version in transitive dependencies
    compile 'com.github.sybila:ode-generator:74d24977ab'
    compile 'com.github.sybila:ctl-model-checker:2.1.2'
    compile 'com.github.sybila:ctl-parser:2.2.3'
    compile 'args4j:args4j:2.33'
    compile files('libs/com.microsoft.z3.jar')
}

//Specify main class
jar {
    manifest {
        attributes  'Implementation-Title': 'Pithya',
                'Implementation-Version': version,
                'Main-Class': 'com.github.sybila.biodivine.StarterKt'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
    void
}

//travis has some strict RAM restrictions, can't afford to exceed this
tasks.withType(Test) {
    maxHeapSize = "1024m"
    jvmArgs "-Xmx1024m"
}

// bundle settings
mainClassName = "com.github.sybila.biodivine.exe.MainKt"
applicationName = "pithya"

task abstractionScript(type: CreateStartScripts) {
    mainClassName = "com.github.sybila.biodivine.exe.ApproximationKt"
    applicationName = "pithyaApproximation"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(abstractionScript)
    fileMode = 0755
}

task guiAbstr(type: CreateStartScripts) {
    mainClassName = "com.github.sybila.biodivine.exe.GUIApproximationKt"
    applicationName = "pithyaGUIapprox"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(guiAbstr)
    fileMode = 0755
}

task guiMain(type: CreateStartScripts) {
    mainClassName = "com.github.sybila.biodivine.exe.GUIMainKt"
    applicationName = "pithyaGUImain"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(guiMain)
    fileMode = 0755
}

task runBench(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.github.sybila.biodivine.exe.BenchKt"
}

task runIdealScale(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.github.sybila.biodivine.exe.IdealScaleKt"
    args = ["4"]
}